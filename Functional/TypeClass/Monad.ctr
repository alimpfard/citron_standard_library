import Library/Control/Error/NotImplementedError.

var identity is {\:x x}.
var pure is {:x
  ^Reflect objectExists: 'thisContext', either: {
    ^thisContext unit: x.
  } or: x.
}. #Ugly HACK to get contextual awareness
pure learn: '$' means: 'applyTo:'.
var Monad is Object new
  on: 'bind:' do: {
    thisBlock error: NotImplementedError $ 'Monad#bind:'.
  },
  on: 'unit:' do: {:value
    ^me cnew: {value => value.}.
  },
  on: 'lift:' do: {:self:func
    ^me bind: \:x self unit: (func applyTo: x).
  },
  on: 'join' do: {
    ^me bind: const identity.
  },
  on: '>>=' do: {:func
    ^me bind: func.
  },
  on: '>' do: {:next
    ^me bind: \:_ next.
  },
  on: '>>' do: {:next
    ^me > next.
  }.

var >>= is \:a:b a bind: b.
var >> is \:a:b a bind: \:_ b.
var join is \:a a join.
var lift is \:ctx:fn ctx lift: fn.
var unit is \:ctx:val ctx unit: val.

import export: Monad as: 'Monad'.
import export: identity as: 'identity'.
import export: pure as: 'pure'.
import export: pure as: 'return'.
